TARGET = RtspPlayer
VLC_PATH = ../packages/libvlc-2.2.2
OPENCV_PATH = ../packages/opencv249

# Project setting
QT += core gui multimedia opengl network widgets
CONFIG += debug_and_release build_all
CONFIG -= flat

# Include file(s) (Include the pri file generated by VS-Addin.)
include(RtspPlayer.pri)

INCLUDEPATH += $${VLC_PATH}/include \
               $${OPENCV_PATH}/include \
               ./src

win32 {
    QMAKE_CXXFLAGS += /Zi
    contains(QMAKE_TARGET.arch, x86_64) {
        QMAKE_LFLAGS += /MACHINE:X64
        UI_DIR = ./GeneratedFiles/x64
        RCC_DIR = ./GeneratedFiles/x64
        LIBS += -L$${VLC_PATH}/lib -llibvlc.x64 -llibvlccore.x64
        
        CONFIG(debug, debug|release) {
            DESTDIR = ./bin/x64/Debug
            MOC_DIR = ./GeneratedFiles/x64/Debug
            LIBS += -L$${OPENCV_PATH}/lib -lopencv_core249d -lopencv_highgui249d
            QMAKE_LFLAGS_WINDOWS += /LARGEADDRESSAWARE
        }
        else {
            DESTDIR = ./bin/x64/Release
            LIBS += -L$${OPENCV_PATH}/lib -lopencv_core249 -lopencv_highgui249
            QMAKE_LFLAGS_WINDOWS += /debug /opt:ref /MAP /MAPINFO:EXPORTS /LARGEADDRESSAWARE
        }
    }
    else {
        QMAKE_LFLAGS += /MACHINE:X86
		#Require x86 lib
        #LIBS +=
		CONFIG(debug, debug|release) {
            DESTDIR = Win32/Debug
            QMAKE_LFLAGS_WINDOWS += /LARGEADDRESSAWARE
        }
        else {
            DESTDIR = Win32/Release
            QMAKE_LFLAGS_WINDOWS += /debug /opt:ref /MAP /MAPINFO:EXPORTS /LARGEADDRESSAWARE
        }
    }
} 